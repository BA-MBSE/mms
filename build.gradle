buildscript {
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion"
    }
    repositories {
        maven { url 'https://repo.spring.io/plugins-release' }
        gradlePluginPortal()
        mavenCentral()
    }
}
apply plugin: 'io.spring.dependency-management'

group = 'org.openmbee'
description = 'Structured Data Version Control'

ext.snapshotBuild = version.contains("SNAPSHOT")
ext.releaseBuild = !version.contains("SNAPSHOT")
ext.milestoneBuild = !(snapshotBuild || releaseBuild)

ext {
    commonDependencies = [
        'swagger-annotations'           : 'io.swagger.core.v3:swagger-annotations:2.1.2',
        'hibernate-core'                : 'org.hibernate:hibernate-core:5.4.14.Final',
        'servlet-api'                   : 'javax.servlet:javax.servlet-api:4.0.1',
        'log4j'                         : "org.apache.logging.log4j:log4j-core:$log4jVersion",
        'log4j-api'                     : "org.apache.logging.log4j:log4j-api:$log4jVersion",
        'spring-boot'                   : "org.springframework.boot:spring-boot:$springBootVersion",
        'spring-security-core'          : "org.springframework.security:spring-security-core:$springSecurityVersion",
        'spring-security-config'        : "org.springframework.security:spring-security-config:$springSecurityVersion",
        'spring-security-web'           : "org.springframework.security:spring-security-web:$springSecurityVersion",
        'spring-security-ldap'          : "org.springframework.security:spring-security-ldap:$springSecurityVersion",
        'spring-tx'                     : "org.springframework:spring-tx:$springFrameworkVersion",
        'spring-webmvc'                 : "org.springframework:spring-webmvc:$springFrameworkVersion",
        'spring-aspects'                : "org.springframework:spring-aspects:$springFrameworkVersion",
        'spring-data-commons'           : "org.springframework.data:spring-data-commons:$springDataVersion",
        'spring-data-jpa'               : "org.springframework.data:spring-data-jpa:$springDataVersion",
        'jackson-annotations'           : "com.fasterxml.jackson.core:jackson-annotations:$jacksonVersion",
        'jackson-databind'              : "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion",
        'jackson-datatype-hibernate5'   : "com.fasterxml.jackson.datatype:jackson-datatype-hibernate5:$jacksonVersion",
        'jackson-datatype-jsr310'       : "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$jacksonVersion",
        'spring-boot-starter-test'      : 'org.springframework.boot:spring-boot-starter-test:2.2.4.RELEASE'
    ]
}

subprojects {
    repositories {
        maven { url 'https://repo.spring.io/plugins-release' }
        gradlePluginPortal()
        mavenCentral()
    }

    plugins.withType(JavaPlugin) {
        project.sourceCompatibility = '10'
        project.targetCompatibility = '10'
    }
}
